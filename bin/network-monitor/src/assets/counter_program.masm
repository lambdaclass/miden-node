# Counter program for network monitoring with note authentication
# Storage layout:
# - OWNER_SLOT: authorized wallet account id as [prefix, suffix, 0, 0]
# - COUNTER_SLOT: counter value (u64)

use miden::core::sys
use miden::protocol::active_account
use miden::protocol::native_account
use miden::protocol::active_note
use miden::protocol::account_id
use miden::protocol::tx


const COUNTER_SLOT = word("miden::monitor::counter_contract::counter")
const OWNER_SLOT = word("miden::monitor::counter_contract::owner")

# Increment function with note authentication
# => []
pub proc increment
    # Ensure the note sender matches the authorized wallet.
    push.OWNER_SLOT[0..2] exec.active_account::get_item
    # => [owner_prefix, owner_suffix, 0, 0]

    exec.active_note::get_sender
    # => [sender_prefix, sender_suffix, owner_prefix, owner_suffix, 0, 0]

    exec.account_id::is_equal
    # => [are_equal, 0, 0]

    assert.err="Note sender not authorized" drop drop
    # => []

    push.COUNTER_SLOT[0..2] exec.active_account::get_item
    # => [count, 0, 0, 0]

    push.1 add
    # => [count+1]

    push.COUNTER_SLOT[0..2] exec.native_account::set_item
    # => [count, 0, 0, 0]

    dropw
    # => []
end

# Get the counter (no auth required)
# => [count]
pub proc get_count
    push.COUNTER_SLOT[0..2] exec.active_account::get_item
    # => [count, 0, 0, 0]

    exec.sys::truncate_stack
    # => [count]
end
